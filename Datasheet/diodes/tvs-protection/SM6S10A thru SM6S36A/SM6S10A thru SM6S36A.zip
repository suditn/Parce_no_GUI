*produced by Vishay General Semiconductor Taiwan
*Allen Su, 2019/11/28
*   PSPICE
*                    anode     cathode
* Reverse direction: node 9 <- node 2
.SUBCKT sm6s22a 9 2
DF 9 90 DFMOD
.MODEL DFMOD D N = 2 IS = 1E-008 RS = 0.00436848
+ EG = 1.11 XTI = 3
+ CJO = 8.12E-009 VJ = 3.58151 M = 0.6 FC = 0.5
+ TT = 1E-008 T_MEASURED = 25
* Leakage
RL 9 90 RLMOD 2.2E6
.MODEL RLMOD Res TC1 = 0 TC2 = 0 T_MEASURED = 25
* Breakdown
RBD 90 4 0.0758
EBD 4 3 10 20 1
DBD 3 9 DBDMOD
.MODEL DBDMOD D IS = 1E-015 N = 0.5 T_MEASURED = 25
IBVC 0 10 0.001
RBVC 10 0 RBVCMOD 25650
.MODEL RBVCMOD Res TC1 = 0.00104 T_MEASURED = 25
RBDX 20 23 0.0758
DBVC 23 0 DBDMOD
IBVD 0 20 0.005
L1 90 2 1E-015
.ENDS
