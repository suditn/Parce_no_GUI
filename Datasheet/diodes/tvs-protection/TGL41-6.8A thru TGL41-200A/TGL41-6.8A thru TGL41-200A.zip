*produced by Vishay General Semiconductor Taiwan
*Allen Su, 2015/10/14
*   PSPICE
*                    anode     cathode
* Reverse direction: node 9 <- node 2
.SUBCKT tgl41-51a 9 2
DF 9 90 DFMOD
.MODEL DFMOD D N = 2 IS = 1E-009 RS = 0.0767906
+ EG = 1.11 XTI = 3
+ CJO = 9.5E-010 VJ = 3.72112 M = 0.6 FC = 0.5
+ TT = 1E-008 T_MEASURED = 25
* Leakage
RL 9 90 RLMOD 8.72E+006
.MODEL RLMOD R TC1 = 0 TC2 = 0 T_MEASURED = 25
* Breakdown
RBD 90 4 3.3421
EBD 4 3 10 20 1
DBD 3 9 DBDMOD TEMP = 25
.MODEL DBDMOD D IS = 1E-015 N = 0.5 T_MEASURED = 25
IBVC 0 10 0.001
RBVC 10 0 51050 RBVCMOD
.MODEL RBVCMOD R TC1 = 0 T_MEASURED = 25
RBDX 20 23 3.3421
DBVC 23 0 DBDMOD
IBVD 0 20 0.001
L1 90 2 1E-015
.ENDS
