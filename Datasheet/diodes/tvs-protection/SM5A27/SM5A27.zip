***********************************************************
Copyright 2020 Vishay Intertechnology, Inc.
All rights reserved.
***********************************************************
*produced by Vishay General Semiconductor Taiwan
*Allen Su, 2016/6/7
*   PSPICE
*                    anode     cathode
* Reverse direction: node 9 <- node 2
.SUBCKT sm5a27 9 2
DF 9 90 DFMOD
.MODEL DFMOD D N = 0.5 IS = 2.28E-024 RS = 0.009
+ EG = 1.11 XTI = 2.6755
+ CJO = 3.9E-009 VJ = 4.44 M = 0.35 FC = 0.5
+ TT = 1E-008 T_MEASURED = 25
* Leakage
RL 9 90 RLMOD 1.1E+008
.MODEL RLMOD RES TC1 = 0 TC2 = 0 T_MEASURED = 25
* Breakdown
RBD 90 4 0.2364
EBD 4 3 10 20 1
DBD 3 9 DBDMOD
.MODEL DBDMOD D IS = 1E-015 N = 0.5 T_MEASURED = 25 T_ABS = 25
IBVC 0 10 0.001
RBVC 10 0 RBVCMOD 27000
.MODEL RBVCMOD RES TC1 = 0 T_MEASURED = 25
RBDX 20 23 0.2364
DBVC 23 0 DBDMOD
IBVD 0 20 0.01
L1 90 2 1E-015
.ENDS
