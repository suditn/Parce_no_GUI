* produced by Allen Su 2020/10/22
*   PSPICE
*                    anode     cathode
* Reverse direction: node 9 <- node 2
.SUBCKT sm8s10a 9 2
DF 9 90 DFMOD
.MODEL DFMOD D N = 2 IS = 1E-008 RS = 0.00436848
+ EG = 1.11 XTI = 3
+ CJO = 1.55E-008 VJ = 0.646306 M = 0.35 FC = 0.5
+ TT = 1E-008 T_MEASURED = 25
* Leakage
RL 9 90 RLMOD 0.67E6
.MODEL RLMOD RES TC1 = 0 TC2 = 0 T_MEASURED = 25
* Breakdown
RBD 90 4 0.014
EBD 4 3 10 20 1
DBD 3 9 DBDMOD
.MODEL DBDMOD D IS = 1E-015 N = 0.5 T_MEASURED = 25 T_ABS = 25
IBVC 0 10 0.001
RBVC 10 0 RBVCMOD 11700
.MODEL RBVCMOD RES TC1 = 0 T_MEASURED = 25
RBDX 20 23 0.014
DBVC 23 0 DBDMOD
IBVD 0 20 0.005
L1 90 2 1E-015
.ENDS
