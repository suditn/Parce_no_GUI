*produced by Vishay General Semiconductor Taiwan
*Allen Su, 2020/11/3
*   PSPICE
*                    anode     cathode
* Reverse direction: node 9 <- node 2
.SUBCKT 1p5ke51a 9 2
DF 9 90 DFMOD
.MODEL DFMOD D N = 1.90263 IS = 7.9183E-008 RS = 0.00753598
+ EG = 1.11 XTI = 3
+ CJO = 1100p VJ = 4.70628 M = 0.6 FC = 0.5
+ TT = 1E-008 T_MEASURED = 25
* Leakage
RL 9 90 RLMOD 43.6e6
.MODEL RLMOD RES TC1 = 0 TC2 = 0 T_MEASURED = 25
* Breakdown
RBD 90 4 0.89
EBD 4 3 10 20 1
DBD 3 9 DBDMOD
.MODEL DBDMOD D IS = 1E-015 N = 0.5 T_MEASURED = 25 T_ABS = 25
IBVC 0 10 0.001
RBVC 10 0 RBVCMOD 51000
.MODEL RBVCMOD RES TC1 = 0.00102 T_MEASURED = 25
RBDX 20 23 0.89
DBVC 23 0 DBDMOD
IBVD 0 20 0.001
L1 90 2 1E-015
.ENDS
